use std;
use math;

#const __FILENAME__         "complexMath"
#const __FILENAME_FULL__    "complexMath.0p"
#const __LIB_NAME__         "ComplexMath"

#const IM_SUFFIX            "í"
#const PLUS_SYM             "+"
#const MINUS_SYM            "-"
#const DOT_SYM              "."
#const COLON_SYM            ":"
#const LEFT_PAREN_SYM       "("
#const RIGHT_PAREN_SYM      ")"
#const STRING_EMPTY         ""

#const NaN void

// Begin __COMPLEXMATH_LIB_EXPORTS__
#const __COMPLEXMATH_LIB_EXPORTS__() => {

    MAKE_EXCEPTION_MSG = (idArr, exceptionMsg) => {
        i = 0;
        len = idArr.length();
        idPath = STRING_EMPTY;
        while (i < len) {
            idPath += idArr[i];
            if (i != len - 1) {
                idPath += DOT_SYM;
            }
            i += 1;
        }
        return idPath + COLON_SYM + " " + exceptionMsg;
    };

    conjugate = x => Complex(x.getReal(), Math.negate(x.getImaginary()));

    reciprocal = x => ((x.getReal() == 0) && (x.getImaginary() == 0)) 
        ? Complex(.0, .0) 
        : std.isInteger(x.getReal()) || std.isInteger(x.getImaginary())
            ? Complex(1.0, .0) / Complex(x.getReal() + .0, x.getImaginary() + .0)
            : Complex(1.0, .0) / x;

    abs = x => {
        c = Math.abs(x.getReal());
        d = Math.abs(x.getImaginary());

        val = NaN;
        if (c > d) {
            r = d / c;
            val = c * Math.sqrt(1.0 + r * r);
        }
        else if (d == .0) {
            val = c;
        }
        else {
            r = c / d;
            val = d * Math.sqrt(1.0 + r * r);
        }

        // abs of complex number always becomes real number but we still want to work with objects
        return Complex(val, 0.0);
    };

    round = (x, scale) => Complex(Math.round(x.getReal(), scale), Math.round(x.getImaginary(), scale));

    round += x => round(x, 0);

    negate = (x) => Complex(Math.negate(x.getReal()), Math.negate(x.getImaginary()));

#const COMPLEX_DEFAULT Complex(.0, .0)
    //
    // Represents a complex number (a+bí)
    // parameters:
    //  real:       The real part of the complex number
    //  imaginary:  The imaginary (í) part of the complex number which 
    //              represents a number times the square root of -1
    //
    Complex = (real, imaginary) => {

        if (!std.isNumber(real)) {
            throw MAKE_EXCEPTION_MSG([__LIB_NAME__, nameof(Complex)], "Real part of complex must be a number");
        }
        if (!std.isNumber(imaginary)) {
            throw MAKE_EXCEPTION_MSG([__LIB_NAME__, nameof(Complex)], "Imaginary part of complex must be a number");
        }

        toString = () => {
            sb = LEFT_PAREN_SYM;
            if (real == 0 && imaginary == 0) {
                sb += "0";
            }
            else {
                hasRe = real != 0;
                hasIm = imaginary != 0;
                if (hasRe) {
                    sb += real + "";
                    if (hasIm) {
                        sb += imaginary < 0 ? MINUS_SYM : PLUS_SYM;
                        sb += (imaginary == 1 || imaginary == -1) ? 
                            IM_SUFFIX : 
                            Math.abs(imaginary) + IM_SUFFIX;
                    }
                }
                // !hasRe && hasIm
                else {
                    if (imaginary < 0) {
                        sb += MINUS_SYM;
                    }
                    sb += (imaginary == 1 || imaginary == -1) ? 
                        IM_SUFFIX : 
                        Math.abs(imaginary) + IM_SUFFIX;
                }
            }
            sb += RIGHT_PAREN_SYM;
            return sb;
        };

        this = {
            getReal = () => real,
            getImaginary = () => imaginary,
            conjugate = conjugate,
            toString = toString,

            // Operator overloads
            operator left[+]right =>
                // if right operand is string, return string
                std.isStringObject(right) ?
                    (left.toString() + right) :
                    Complex(left.getReal() + right.getReal(), left.getImaginary() + right.getImaginary()),

            operator left[-]right => Complex(left.getReal() - right.getReal(), left.getImaginary() - right.getImaginary()),

            operator left[*]right => {
                reProd = (left.getReal() * right.getReal()) - (left.getImaginary() * right.getImaginary());
                imProd = (left.getImaginary() * right.getReal()) + (left.getReal() * right.getImaginary());
                return Complex(reProd, imProd);
            },

            operator left[/]right => {
                a = left.getReal();
                b = left.getImaginary();
                c = right.getReal();
                d = right.getImaginary();

                complex = NaN;
                if (Math.abs(d) < Math.abs(c)) {
                    dcQuot = d / c;
                    complex = Complex((a + b * dcQuot) / (c + d * dcQuot), (b - a * dcQuot) / (c + d * dcQuot));
                }
                else {
                    cdQuot = c / d;
                    complex = Complex((b + a * cdQuot) / (d + c * cdQuot), (-a + b * cdQuot) / (d + c * cdQuot));
                }

                return complex;
            },

            operator left[==]right => {
                return right == NaN ? 
                    false : 
                    (left.getReal() == right.getReal()) && 
                        (left.getImaginary() == right.getImaginary());
            },

            operator left[!=]right => {
                return right == NaN ?
                    true :
                    (left.getReal() != right.getReal()) ||
                        (left.getImaginary() != right.getImaginary());
            },

            operator unary [-]x => {
                re = x.getReal();
                im = x.getImaginary();
                return Complex(-re, -im);
            }
        };

        return this;
    };

    Complex += real => {
        if (!std.isNumber(real)) {
            throw MAKE_EXCEPTION_MSG([__LIB_NAME__, nameof(Complex)], "Real part of complex must be a number");
        }

        return std.isDecimal(real) ? Complex(real, .0) : Complex(real, 0);
    };

    Complex += () => COMPLEX_DEFAULT;

    return {
        Complex = Complex,
        conjugate = conjugate,
        reciprocal = reciprocal,
        abs = abs,
        round = round,
        negate = negate
    };
} // End __COMPLEXMATH_LIB_EXPORTS__

ComplexMath = __COMPLEXMATH_LIB_EXPORTS__;
ComplexMath = ComplexMath();
